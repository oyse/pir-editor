grammar org.dadacoalition.pireditor.Pir hidden( NONE_NL_WS, COMMENT, POD_COMMENT, NL ) 

generate pir "http://www.dadacoalition.org/pireditor/Pir"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore



File:
    (
        statements+=Statement |
        subroutines+=SubDef
    )* ;
    
Statement hidden( NONE_NL_WS, COMMENT, POD_COMMENT ):
    ( directives+=Directive |
      (label=Label ':')? instructions+=Instruction ) NL;

Directive:
    LocalDirective |
    NamespaceDirective |
    LexDirective |
    ConstDirective |
    LoadlibDirective |
    HLLDirective |
    LineDirective |
    FileDirective |
    AnnotateDirective |
    IncludeDirective;    


Instruction:
    Assignment |
    AttributeAssignment |
    OpcodeInvocation |
    FunctionCall |
    MethodCall;

Assignment:
    (VARIABLE|NamedVariable) AssignmentOperator 
        ( VARIABLE |
          Constant |
          UnaryOperator VARIABLE |
          VARIABLE BinaryOperator VARIABLE |
          VARIABLE '[' VARIABLE ']' |
          FunctionCall |
          MethodCall |
          OpcodeInvocation
    );


AssignmentOperator:
    '=' | '+=' | '-=' | '*=' | '/=' | '%=' | '.=' | '&=' '|=' | '~=' | '<<=' | '>>=' | '>>>='; 

AttributeAssignment:
    VARIABLE '[' VARIABLE ']' AssignmentOperator VARIABLE;

FunctionCall:
    STRING | PARROTID '(' (Argument (',' Argument)?)? ')';

MethodCall:
    'Test';

OpcodeInvocation:
    Opcode (OpcodeArgument (',' OpcodeArgument)?)?;
    
Opcode:
    PARROTID;

OpcodeArgument:
    VARIABLE | Constant | Namespace;

Argument:
    VARIABLE ArgumentModifier? | Namespace;

ArgumentModifier:
    'Test';

UnaryOperator:
    '!' | '-' | '~'; 

BinaryOperator:
    '+' | '-' | '*' | '/' | '%' | '**' |
    '.' |
    '<<' | '>>' | '>>>' |
    '&&' | '||' | '~~' |
    '&' | '|' | '~' |
    RelationOperator;

RelationOperator:
    '<' | '<=' | '==' | '!=' | '>=' | '>';
    
    
Label:
    PARROTID;

// newlines are relevant for subs
SubDef hidden( NONE_NL_WS, COMMENT, POD_COMMENT):
    '.sub' name=(PARROTID|STRING) (':'modifiers+=SubModifier)* NL+
    (statements+=Statement)*   
    NL? '.end';

SubModifier:
    'main' |
    'load' |
    'init' |
    'anon' |
    'immediate' |
    'postcomp' |
    'method' '(' ')' |
    'vtable' |
    'outer' '(' PARROTID ')' |
    'subid' '(' STRING ')' |
    'instanceof' '(' STRING ')' |
    'nsentry' '(' STRING ')' 
    ;

LocalDirective :
    '.local' type=Type NamedVariable (',' NamedVariable)* ':uniquereq'?;
    
IncludeDirective :
    '.include' STRING;

NamespaceDirective:
    '.namespace' Namespace;    

Namespace:
    '[' (STRING (';' STRING)?)? ']';

LexDirective:
    '.lex' STRING '$P' INT;

ConstDirective:
    '.const' Type PARROTID '=' Constant;

LoadlibDirective:
    '.loadlib' STRING;
    
HLLDirective:
    '.HLL' STRING;
    
LineDirective:
    '.line' INT;
    
FileDirective:
    '.file' STRING;
    
AnnotateDirective:
    '.annotate' STRING Constant;
    
Constant:
    ((':' Encoding)?':' Charset)? STRING |    
    INT;

Charset:
    'ascii' | 'binary' | 'unicode' | 'iso-8859-1';

Encoding:
    'utf8';    
    
Type :
    'int' |
    'num' |
    'string' |
    'pmc';

NamedVariable :
    PARROTID;

Keywords:
    'goto'   |
    'if'     |
    'int'    |
    'null'   | 
    'num'    |
    'pmc'    |
    'string' |
    'unless';
    
terminal COMMENT :
    '#' !('\n'|'\r')* ('\r'? '\n')?;   

terminal POD_COMMENT :
    ('=pod'      |
     '=head1'    |
     '=head2'    |
     '=head3'    |
     '=head4'    |
     '=over'     |
     '=item'     |
     '=back'     |
     '=begin'    |
     '=end'      |
     '=for'      |
     '=encoding') -> 
    '=cut' NL;
    
terminal NL:
    ('\r'? '\n');

terminal VARIABLE:
    '$P' '0'..'9'+ |
    '$I' '0'..'9'+ |
    '$N' '0'..'9'+ |
    '$S' '0'..'9'+ ;
   
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal PARROTID :  
    ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
    ;

terminal STRING: 
            '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
            "'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
        ; 

terminal NONE_NL_WS:
    (' '|'\t' )+; 

terminal ANY_OTHER: 
    .;    